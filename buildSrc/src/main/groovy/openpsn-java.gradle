plugins {
    id 'java'
}

group = 'org.openpsn'
version = '0.1.0-SNAPSHOT'

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    // Always re-run tests
    outputs.upToDateWhen { false }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter
    testImplementation libs.mockito.core
    testImplementation libs.mockito.jupiter

    integrationTestImplementation platform(libs.junit.bom)
    integrationTestImplementation libs.junit.jupiter
    integrationTestImplementation libs.mockito.core
    integrationTestImplementation libs.mockito.jupiter
}
